import org.apache.tools.ant.taskdefs.condition.Os

jar.enabled = false

group = 'com.liferay.sample'
version = '1.0.0-SNAPSHOT'

description = """Sample Functional Test Module"""

repositories {
	jcenter()
	mavenCentral()
	mavenLocal()
}

project.ext {
	cucumberVersion = '1.2.5'
}

dependencies {
	compile group: "com.liferay.portal", name: "com.liferay.portal.kernel", version: "2.0.0"
	compile group: "javax.portlet", name: "portlet-api", version: "2.0"
	compile group: "javax.servlet", name: "servlet-api", version: "2.5"
	compile group: "org.codehaus.groovy", name: "groovy-all", version: "2.4.5"
	compile group: "org.osgi", name: "org.osgi.compendium", version: "5.0.0"
}

tasks.withType(Test) {
	testLogging {
		events "passed", "skipped", "failed"
		showExceptions true
		exceptionFormat "short"
		showCauses true
		showStackTraces true

		afterSuite { desc, result ->
			if (!desc.parent) {
				def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
				def startItem = '|  ', endItem = '  |'
				def repeatLength = startItem.length() + output.length() + endItem.length()
				println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
			}
		}
	}
}

tasks.withType(Test) {
	reports.html.destination = file("${reporting.baseDir}/${name}")
}

sourceSets {
	functionalTest {
		java {
			compileClasspath += main.output + test.output
			runtimeClasspath += main.output + test.output
			srcDir file('src/functionalTest/java')
		}
		resources.srcDir file('src/functionalTest/resources')
	}
}

configurations {
	functionalTestCompile.extendsFrom testCompile
	functionalTestRuntime.extendsFrom testRuntime
}

task performTests(type: Test) {
	description 'Run the Functional Tests.'

	testClassesDir = sourceSets.functionalTest.output.classesDir
	classpath = sourceSets.functionalTest.runtimeClasspath

	filter {
		includeTestsMatching "*AllTests"
	}

	outputs.upToDateWhen { false }
}

task runFunctionalTest {
	dependsOn performTests
}

task functionalTest {
	dependsOn rootProject.deployTestableModules, startTestableTomcat, runFunctionalTest
}

startTestableTomcat.mustRunAfter rootProject.deployTestableModules
performTests.mustRunAfter startTestableTomcat
stopTestableTomcat.mustRunAfter functionalTest

// Command to run this task: ./gradlew functionalTestClass -PclassToBeTested=<Class Name>
// Example: ./gradlew functionalTestClass -PclassToBeTested=SampleTest
task functionalTestClass(type: Test) {
	description 'Run the Specific Functional Tests.'

	testClassesDir = sourceSets.functionalTest.output.classesDir
	classpath = sourceSets.functionalTest.runtimeClasspath

	scanForTestClasses = false

	if (project.hasProperty('classToBeTested')==false){
	} else {
		includes = ['**/'+project.getProperty('classToBeTested')+'.class']
	}

	outputs.upToDateWhen { false }
}

dependencies {
	functionalTestCompile(
			'info.cukes:cucumber-core:' + cucumberVersion,
			'info.cukes:cucumber-java:' + cucumberVersion,
			'info.cukes:cucumber-java:' + cucumberVersion,
			'info.cukes:cucumber-junit:' + cucumberVersion,
			'info.cukes:cucumber-spring:' + cucumberVersion,
			'org.springframework:spring-beans:4.2.5.RELEASE'
	)
	functionalTestRuntime(
			'org.springframework:spring-context:4.2.5.RELEASE',
			'org.springframework:spring-test:4.2.5.RELEASE',
			'org.springframework:spring-tx:4.2.5.RELEASE'
	)
}

task setupFunctionalTest {
	description 'Create the Functional Tests properties and driver, but only work if run on project root folder.'

	doLast {
		def baseDir = "${project.hasProperty('liferay.workspace.home.dir') ? project.property('liferay.workspace.home.dir') : 'modules'}"
		def SeleniumPropertyKeysPath = new File('modules/tests/functional-tests/SeleniumProperties')
		def SeleniumPropertyKeysFile = new File('modules/tests/functional-tests/SeleniumProperties/SeleniumPropertyKeys.properties')
		println "You should run this task only on your project root folder"
		if (!SeleniumPropertyKeysFile.exists()) {
			println "Create Configuration for use a local SeleniumPropertyKeys.properties"
			SeleniumPropertyKeysPath.mkdirs()
			SeleniumPropertyKeysFile.withWriterAppend { w ->
				w << "browser=defaultGC\ntime-out=20"
			}
		} else {
			println "You already had the local SeleniumPropertyKeys.properties created"
		}

		if ((Os.isFamily(Os.FAMILY_WINDOWS)) && (!new File(baseDir + '/tests/functional-tests/SeleniumProperties/chromedriver.exe').exists())) {
			println "Create Configuration for Windows to use the Google Chrome Driver"
			download {
				src 'https://chromedriver.storage.googleapis.com/2.34/chromedriver_win32.zip'
				dest new File(SeleniumPropertyKeysPath, 'chromedriver_win32.zip')
			}

			copy {
				def DriverPath = new File('/SeleniumProperties/')
				from zipTree('/SeleniumProperties/chromedriver_win32.zip')
				into "SeleniumProperties/"
			}
			delete 'SeleniumProperties/chromedriver_win32.zip'

		} else if ((Os.isFamily(Os.FAMILY_MAC)) && (!new File(baseDir + '/tests/functional-tests/SeleniumProperties/chromedriver').exists())) {
			println "Create Configuration for Mac to use the Google Chrome Driver"
			download {
				src 'https://chromedriver.storage.googleapis.com/2.34/chromedriver_mac64.zip'
				dest new File(SeleniumPropertyKeysPath, 'chromedriver_mac64.zip')
			}

			copy {
				def DriverPath = new File('/SeleniumProperties/')
				from zipTree('SeleniumProperties/chromedriver_mac64.zip')
				into "SeleniumProperties/"
			}
			delete 'SeleniumProperties/chromedriver_mac64.zip'

		} else if ((Os.isFamily(Os.FAMILY_UNIX)) && (!new File(baseDir + '/tests/functional-tests/SeleniumProperties/chromedriver').exists())) {
			println "Create Configuration for Unix to use the Google Chrome Driver"
			download {
				src 'https://chromedriver.storage.googleapis.com/2.34/chromedriver_linux64.zip'
				dest new File(SeleniumPropertyKeysPath, 'chromedriver_linux64.zip')
			}

			copy {
				from zipTree(new File('SeleniumProperties/', 'chromedriver_linux64.zip'))
				into "SeleniumProperties/"
			}
			delete 'SeleniumProperties/chromedriver_linux64.zip'
		} else {
			println "You already had the local Google Chrome Driver created"
		}
	}
}